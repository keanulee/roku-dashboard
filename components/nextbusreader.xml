<?xml version="1.0" encoding="utf-8" ?> 

<component name="NextBusReader" extends="Task">
 
  <interface>
    <field id="content" type="node" />
    <field id="messages" type="string" />
  </interface> 

  <script type="text/brightscript">

    <![CDATA[

    sub init()
      m.top.functionName = "getcontent"
    end sub

    sub getcontent()
      readInternet = createObject("roUrlTransfer")
      readInternet.setUrl("http://webservices.nextbus.com/service/publicXMLFeed?command=predictions&a=sf-muni&r=L&stopId=16627")
      contentxml = createObject("roXMLElement")
      contentxml.parse(readInternet.GetToString())
      content = createObject("roSGNode", "ContentNode")

      if contentxml.getName()="body"
        for each predictions in contentxml.GetNamedElements("predictions")
          section = content.createChild("ContentNode")
          section.contenttype = "SECTION"
          item = section.createChild("ContentNode")
          item.title = predictions.getAttributes()["routeTitle"]
          for each direction in predictions.GetNamedElements("direction")
            section = content.createChild("ContentNode")
            section.contenttype = "SECTION"
            section.title = mid(direction.getAttributes()["title"], 12)
            times = []
            for each prediction in direction.GetNamedElements("prediction")
              times.push(prediction.getAttributes()["minutes"])
            end for
            item = section.createChild("ContentNode")
            item.title = times.join(", ")
          end for

          messages = []
          for each message in predictions.GetNamedElements("message")
            messages.push(message.getAttributes()["text"])
          end for
          m.top.messages = messages.join("  â€¢  ").replace(chr(10), " ")
        end for
      end if

      m.top.content = content
    end sub

    ]]>

  </script>

</component>
