<?xml version="1.0" encoding="utf-8" ?>

<component name="MainScene" extends="OverhangPanelSetScene">

  <script type="text/brightscript">

    <![CDATA[

    sub init()
      m.top.backgroundColor="0x000000FF"
      m.top.backgroundUri = ""
      m.top.setFocus(true)
      setVideo()

      m.label = m.top.findNode("label")
      m.labellist = m.top.findNode("labelList")
      m.scrollinglabel = m.top.findNode("scrollingLabel")
      m.timer = m.top.findNode("timer")
      m.timer.control = "start"
      m.timer.ObserveField("fire", "fetchNextBus")
      fetchNextBus()
      fetchWeather()
    end sub

    sub fetchNextBus()
      m.nextBusReaderTask = createObject("RoSGNode", "NextBusReader")
      m.nextBusReaderTask.observeField("content", "showNextBus")
      m.nextBusReaderTask.observeField("messages", "showNextBusMessages")
      m.nextBusReaderTask.control = "RUN"
    end sub

    sub showNextBus()
      m.labellist.content = m.nextBusReaderTask.content
    end sub

    sub showNextBusMessages()
      m.scrollinglabel.text = m.nextBusReaderTask.messages
    end sub

    sub fetchWeather()
      m.weatherReaderTask = createObject("RoSGNode", "WeatherReader")
      m.weatherReaderTask.observeField("text", "showWeather")
      m.weatherReaderTask.control = "RUN"
    end sub

    sub showWeather()
      m.top.overhang.title = m.weatherReaderTask.text
    end sub
    
    function setVideo() as void
      videocontent = createObject("RoSGNode", "ContentNode")
      videocontent.title = "News"
      videocontent.streamformat = "hls"
      videocontent.url = "http://dcs-live.apis.anvato.net/server/play/10r3b2gtagOkUyAg/1926000/prog.m3u8?x=8825&fw_mvpd=d41d8cd98f00b204e9800998ecf8427e&fw_csid=&fw_caid=&anv_user=b5e1218bed4dc479146f54bd6206b6f731903&fw_prof=&fw_network=&fw_ltlg=&fw_mvpd_id=&fw_sdk_flag=%2Bslcb%2Bvicb&fw_metr=7&_dev=web&fw_did=40ABF359-1C59-4C52-9E7D-288A5969E899&fw_sdk_flag_safe=%252Bslcb%252Bvicb&id=40ABF359-1C59-4C52-9E7D-288A5969E899&rdid=40ABF359-1C59-4C52-9E7D-288A5969E899&is_lat=0&_tkx_anvack=anvato_epfox_app_web_prod_b3373168e12f423f41504f207000188daf88251b&_tkx_callsign=adstajo9vmxqGgkD&t=1522731689&_shost=176820210&_fp=31fd4ee8296fcb58c4e9505bfec67bfb&anvsid=i176820210-n98370b67-08eb-4d82-a442-90150561a0dc"

      video = m.top.findNode("video")
      video.content = videocontent
      video.control = "play"
      video.setFocus(true)
    end function

    ]]>

  </script>

  <children>
 
    <Video
      id="video"
      width="1000"
      height="600"
      translation="[0, 115]" />

    <LabelList
      id="labelList"
      drawFocusFeedback="false"
      sectionDividerMinWidth="0"
      translation="[1010, 115]"
      itemSize="[270, 60]" />
    
    <ScrollingLabel
      id="scrollingLabel"
      maxwidth="270"
      height="60"
      translation="[1010, 660]"
      text="No messages" />

    <Timer
      id="timer"
      repeat="true"
      duration="30" />

  </children>

</component>
